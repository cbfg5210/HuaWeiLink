import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.huaweilink"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        resConfigs "zh-rCN"
    }

    signingConfigs {
        release {
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
            storeFile file(RELEASE_FILE_PATH)
            storePassword RELEASE_STORE_PASSWORD
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles file('proguards').listFiles(new FilenameFilter() {
                @Override
                boolean accept(File dir, String name) {
                    return name.endsWith('.pro')
                }
            })
            signingConfig signingConfigs.release
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a", "x86"
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles file('proguards').listFiles(new FilenameFilter() {
                @Override
                boolean accept(File dir, String name) {
                    return name.endsWith('.pro')
                }
            })
            signingConfig signingConfigs.release
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //用java1.8编译kotlin
    kotlinOptions {
        jvmTarget = "1.8"
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == 'release') {
                def date = new SimpleDateFormat("MMddHHmm").format(System.currentTimeMillis())
                outputFileName = "HuaWeiLink_v${output.versionNameOverride}_${output.versionCodeOverride}_${date}.apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.20'
    implementation 'com.gitee.cbfg5210:RVAdapter:0.3'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
}

afterEvaluate {
    generateDebugBuildConfig.enabled = false
    generateReleaseBuildConfig.enabled = false
}